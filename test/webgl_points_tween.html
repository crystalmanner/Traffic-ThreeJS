<!DOCTYPE html>
<html lang="en">
    <head>
        <title>three.js webgl - materials - shaders [lava]</title>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
        <style>
            body {
                color: #ffffff;
                font-family:Monospace;
                font-size:13px;
                text-align:center;
                font-weight: bold;

                background-color: #000000;
                margin: 0px;
                overflow: hidden;
            }

            #info {
                position: absolute;
                top: 0px; width: 100%;
                padding: 5px;
            }

            a {

                color: #ffffff;
            }

            #oldie a { color:#da0 }
        </style>
    </head>
    <body>

        <div id="container"></div>
        <div id="info"><a href="http://threejs.org" target="_blank">three.js</a> - points tween shader demo by <a href="https://github.com/lo-th" target="_blank">lo.th</a></div>

        <script src="js/three.min.js"></script>

        <script src="js/Detector.js"></script>
        <script src="js/libs/stats.min.js"></script>

        <script id="fragmentShader" type="x-shader/x-fragment">

            uniform sampler2D map;
            varying float vAlpha;
            varying vec3 pos;

            void main(){
                
                vec4 color = vec4( 1.0-pos.x , pos.x , pos.y , vAlpha);
                gl_FragColor = color;
                if ( gl_FragColor.a <= 0.001 ) discard;

            }

        </script>

        <script id="vertexShader" type="x-shader/x-vertex">

            #define RGB 0.0039215686274509803921568627451
            #define PI_90 1.570796326794896
            #define M_PI 3.14159265358979323846
            #define TWO_PI 6.28318530717958647692
            #define PHASE_TIME 0.1666666666666666667
            #define DEG_TO_RAD 0.0174532925199432957

            ////////////////////////////
            //       TWEEN BASE       //
            ////////////////////////////

            

            // LINEAR
            float linear( float k ) { return k; }
            // QUAD
            float inQuad(float k) { return k * k; }
            float outQuad(float k) { return k * ( 2.0 - k );}
            float inOutQuad(float k) {
                if ( ( k *= 2.0 ) < 1.0 ) return 0.5 * k * k;
                return - 0.5 * ( --k * ( k - 2.0 ) - 1.0 );
            }
            // CUBIC
            float inCubic(float k) { return k * k * k; }
            float outCubic(float k) { return --k * k * k + 1.0; }
            float inOutCubic(float k) {
                if ( ( k *= 2.0 ) < 1.0 ) return 0.5 * k * k * k;
                return 0.5 * ( ( k -= 2.0 ) * k * k + 2.0 );
            }
            // QUART
            float inQuart(float k) { return k * k * k * k; }
            float outQuart(float k) { return 1.0 - ( --k * k * k * k ); }
            float inOutQuart(float k) {
                if ( ( k *= 2.0 ) < 1.0) return 0.5 * k * k * k * k;
                return - 0.5 * ( ( k -= 2.0 ) * k * k * k - 2.0 );
            }
            // QUINT
            float inQuint(float k) { return k * k * k * k * k; }
            float outQuint(float k) { return --k * k * k * k * k + 1.0; }
            float inOutQuint(float k) {
                if ( ( k *= 2.0 ) < 1.0 ) return 0.5 * k * k * k * k * k;
                return 0.5 * ( ( k -= 2.0 ) * k * k * k * k + 2.0 );
            }
            // SINE
            float inSine(float k) { float j = k * PI_90; return 1.0 - cos( j ); }
            float outSine(float k) { float j = k * PI_90; return sin( j ); }
            float inOutSine(float k) { float j = k * M_PI; return 0.5 * ( 1.0 - cos( j ) ); }
            // EXPO
            float inExpo(float k) { return k == 0.0 ? 0.0 : pow( 1024.0, k - 1.0 ); }
            float outExpo(float k) { return k == 1.0 ? 1.0 : 1.0 - pow( 2.0, - 10.0 * k ); }
            float inOutExpo(float k) {
                if ( k == 0.0 ) return 0.0;
                if ( k == 1.0 ) return 1.0;
                if ( ( k *= 2.0 ) < 1.0 ) return 0.5 * pow( 1024.0, k - 1.0 );
                return 0.5 * ( - pow( 2.0, - 10.0 * ( k - 1.0 ) ) + 2.0 );
            }
            // CIRC
            float inCirc(float k) { return 1.0 - sqrt( 1.0 - k * k ); }
            float outCirc(float k) { return sqrt( 1.0 - ( --k * k ) ); }
            float inOutCirc(float k) {
                if ( ( k *= 2.0 ) < 1.0) return - 0.5 * ( sqrt( 1.0 - k * k ) - 1.0 );
                return 0.5 * ( sqrt( 1.0 - ( k -= 2.0 ) * k ) + 1.0 );
            }
            // ELASTIC
            float inElastic(float k) {
                float s;
                float a = 0.1;
                float p = 0.4;
                if ( k == 0.0 ) return 0.0;
                if ( k == 1.0 ) return 1.0;
                if ( a < 1.0 ) { a = 1.0; s = p * 0.25; }
                else s = p * asin( 1.0 / a ) / TWO_PI;
                return - ( a * pow( 2.0, 10.0 * ( k -= 1.0 ) ) * sin( ( k - s ) * TWO_PI / p ) );
            }
            float outElastic(float k) {
                float s;
                float a = 0.1; 
                float p = 0.4;
                if ( k == 0.0 ) return 0.0;
                if ( k == 1.0 ) return 1.0;
                if ( a < 1.0 ) { a = 1.0; s = p * 0.25; }
                else s = p * asin( 1.0 / a ) / TWO_PI;
                return ( a * pow( 2.0, - 10.0 * k) * sin( ( k - s ) * TWO_PI / p ) + 1.0 );
            }
            float inOutElastic(float k) {
                float s;
                float a = 0.1;
                float p = 0.4;
                if ( k == 0.0 ) return 0.0;
                if ( k == 1.0 ) return 1.0;
                if ( a < 1.0 ) { a = 1.0; s = p * 0.25; }
                else s = p * asin( 1.0 / a ) / TWO_PI;
                if ( ( k *= 2.0 ) < 1.0 ) return - 0.5 * ( a * pow( 2.0, 10.0 * ( k -= 1.0 ) ) * sin( ( k - s ) * TWO_PI / p ) );
                return a * pow( 2.0, -10.0 * ( k -= 1.0 ) ) * sin( ( k - s ) * TWO_PI / p ) * 0.5 + 1.0;
            }
            // BACK
            float inBack(float k) {
                float s = 1.70158;
                return k * k * ( ( s + 1.0 ) * k - s );
            }
            float outBack(float k) {
              float s = 1.70158;
              return --k * k * ( ( s + 1.0 ) * k + s ) + 1.0;
            }
            float inOutBack(float k) {
              float s = 1.70158 * 1.525;
              if ( ( k *= 2.0 ) < 1.0 ) return 0.5 * ( k * k * ( ( s + 1.0 ) * k - s ) );
              return 0.5 * ( ( k -= 2.0 ) * k * ( ( s + 1.0 ) * k + s ) + 2.0 );
            }
            // BOUNCE
            float outBounce(float k) {
                if ( k < ( 1.0 / 2.75 ) ) return 7.5625 * k * k;
                else if ( k < ( 2.0 / 2.75 ) ) return 7.5625 * ( k -= ( 1.5 / 2.75 ) ) * k + 0.75;
                else if ( k < ( 2.5 / 2.75 ) ) return 7.5625 * ( k -= ( 2.25 / 2.75 ) ) * k + 0.9375;
                else return 7.5625 * ( k -= ( 2.625 / 2.75 ) ) * k + 0.984375;
            }
            float inBounce(float k) { return 1.0 - outBounce( 1.0 - k ); }
            float inOutBounce(float k) {
                if ( k < 0.5 ) return inBounce( k * 2.0 ) * 0.5;
                return outBounce( k * 2.0 - 1.0 ) * 0.5 + 0.5;
            }

            float tween ( int type, float k ){

                if(type == 0) k = linear( k );

                if(type == 1) k = inQuad( k );
                if(type == 2) k = outQuad( k );
                if(type == 3) k = inOutQuad( k );

                if(type == 4) k = inCubic( k );
                if(type == 5) k = outCubic( k );
                if(type == 6) k = inOutCubic( k );

                if(type == 7) k = inQuart( k );
                if(type == 8) k = outQuart( k );
                if(type == 9) k = inOutQuart( k );

                if(type == 10) k = inQuint( k );
                if(type == 11) k = outQuint( k );
                if(type == 12) k = inOutQuint( k );

                if(type == 13) k = inSine( k );
                if(type == 14) k = outSine( k );
                if(type == 15) k = inOutSine( k );

                if(type == 16) k = inExpo( k );
                if(type == 17) k = outExpo( k );
                if(type == 18) k = inOutExpo( k );

                if(type == 19) k = inCirc( k );
                if(type == 20) k = outCirc( k );
                if(type == 21) k = inOutCirc( k );

                if(type == 22) k = inElastic( k );
                if(type == 23) k = outElastic( k );
                if(type == 24) k = inOutElastic( k );

                if(type == 25) k = inBack( k );
                if(type == 26) k = outBack( k );
                if(type == 27) k = inOutBack( k );

                if(type == 28) k = inBounce( k );
                if(type == 29) k = outBounce( k );
                if(type == 30) k = inOutBounce( k );

                return k;

            }

            float lerp( float a, float b, float p ){ return a + (b - a) * p; }
            vec3 posLerp( vec3 p1, vec3 p2, float p ){ return vec3( lerp( p1.x, p2.x, p ), lerp( p1.y, p2.y, p ), lerp( p1.z, p2.z, p ) ); }

            attribute float iTime;
            attribute float size;

            uniform float time;
            uniform int type;

            uniform float scale;
            uniform float pixelRatio;

            varying float vAlpha;
            varying vec3 pos;

            void main(){

                vAlpha = 1.0;
                pos = position;

                float a = tween( type, iTime);
                float b = tween( type, time);

                pos.x = lerp(0.0, 1.0, iTime);
                pos.y = lerp(0.0, 1.0, a);

                if(size == 0.1){
                    pos.x = lerp(0.0, 1.0, time);
                    pos.y = lerp(0.0, 1.0, b);
                }

                vec4 mvPosition = modelViewMatrix * vec4( pos, 1.0 );
                gl_PointSize = pixelRatio * size * ( scale / length( mvPosition.xyz ) );
                gl_Position = projectionMatrix * mvPosition;

            }




        </script>

        <script>

            if ( ! Detector.webgl ) Detector.addGetWebGLMessage();

            var container, stats, speed = 0.01, time = 0, type = 0;

            var camera, scene, renderer, composer;

            var uniforms, material, particles, geometry, positions, times, sizes;

            init();
            animate();

            function init() {

                container = document.getElementById( 'container' );

                camera = new THREE.PerspectiveCamera( 35, 1, 1, 3000 );
                camera.position.z = 3;

                scene = new THREE.Scene();

                uniforms = {

                    scale :  { type: 'f', value: 400.0 },
                    size :  { type: 'f', value: 0.01 },
                    time: { type: "f", value: time },
                    type: { type: "i", value: type },
                    pixelRatio: { type: 'f', value: window.devicePixelRatio },

                };

                material = new THREE.ShaderMaterial( {

                    uniforms: uniforms,
                    vertexShader: document.getElementById( 'vertexShader' ).textContent,
                    fragmentShader: document.getElementById( 'fragmentShader' ).textContent

                } );

                var max = 600;

                geometry = new THREE.BufferGeometry();
                sizes = new Float32Array( max );
                times = new Float32Array( max );
                positions = new Float32Array( max * 3 );
                for(var i = 0; i<max;i++){
                    times[i] = (i/max);
                    sizes[i] = 0.01;

                    if(i == 0) sizes[i] = 0.1;
                }
                geometry.addAttribute( 'position', new THREE.BufferAttribute( positions, 3 ) );
                geometry.addAttribute( 'iTime', new THREE.BufferAttribute( times, 1 ) );
                geometry.addAttribute( 'size', new THREE.BufferAttribute( sizes, 1 ) );

                particles = new THREE.Points( geometry, material );
                particles.position.set(-0.5, -0.5, 0);
                particles.frustumCulled = false;

                scene.add( particles );

                //

                renderer = new THREE.WebGLRenderer( { antialias: true } );
                renderer.setPixelRatio( window.devicePixelRatio );
                container.appendChild( renderer.domElement );

                //

                stats = new Stats();
                stats.domElement.style.position = 'absolute';
                stats.domElement.style.top = '0px';
                
                container.appendChild( stats.domElement );

                resize();

                window.addEventListener( 'resize', resize, false );

            }

            function resize( event ) {

                renderer.setSize( window.innerWidth, window.innerHeight );
                camera.aspect = window.innerWidth / window.innerHeight;
                camera.updateProjectionMatrix();

            }

            //

            function animate() {

                requestAnimationFrame( animate );
                render();
                stats.update();

            }

            function render() {

                time += speed;
                if(time > 1){
                    type ++;
                    type = type == 31 ? 0 : type;
                    uniforms.type.value = type;
                    time = 0;
                }
                //time = time > 1 ? 0 : time;
                uniforms.time.value = time;

                renderer.render( scene, camera );

            }

        </script>

    </body>
</html>
